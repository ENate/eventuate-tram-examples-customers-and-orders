buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "com.avast.gradle:gradle-docker-compose-plugin:0.12.0"
    }
}

apply plugin: 'docker-compose'

subprojects {

    apply plugin: "java"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()

        eventuateMavenRepoUrl.split(',').each { repoUrl -> maven { url repoUrl } }
        maven { url "https://repo.spring.io/snapshot" }
    }

    dependencies {
      implementation(platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")) {
          exclude group: 'org.apache.kafka'
          exclude group: 'ch.qos.logback'
      }

      implementation(platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"))

      implementation 'ch.qos.logback:logback-classic:1.2.9'
      implementation 'ch.qos.logback:logback-core:1.2.9'
    }


}

dockerCompose {
    environment.put "EVENTUATE_COMMON_VERSION", eventuateCommonImageVersion
    environment.put "EVENTUATE_KAFKA_VERSION", eventuateMessagingKafkaImageVersion
    environment.put "EVENTUATE_CDC_VERSION", eventuateCdcImageVersion
    environment.put "EVENTUATE_CDC_KAFKA_ENABLE_BATCH_PROCESSING", eventuateCdcKafkaEnableBatchProcessing
    environment.put "EVENTUATE_JAVA_BASE_IMAGE_VERSION", eventuateExamplesBaseImageVersion

    mysqlbinlog {
        projectName = null
        useComposeFiles = ["docker-compose-mysql-binlog.yml"]
        removeContainers = project.ext.removeContainers
    }

    mysqlbinloginfrastructure {
        projectName = null
        useComposeFiles = ["docker-compose-mysql-binlog.yml"]
        startedServices = ["cdcservice", "zipkin"]
        removeContainers = project.ext.removeContainers
    }

    mysqlbinlogwithjmeter {
        projectName = null
        useComposeFiles = ["docker-compose-mysql-binlog.yml", "docker-compose-jmeter-service.yml"]
        startedServices = ["jmeterservice", "customerservice"]
    }

    mysqlbinlogtextsearch {
        projectName = null
        useComposeFiles = ["docker-compose-snapshots-mysql-binlog.yml"]
        removeContainers = project.ext.removeContainers
    }

    mysqlbinlogtextsearchcustomersandorders {
        projectName = null
        useComposeFiles = ["docker-compose-snapshots-mysql-binlog.yml"]
        startedServices = ["orderservice", "customerservice"]
        removeContainers = project.ext.removeContainers
    }

    mysqlbinloginfrastructuretextsearch {
        projectName = null
        useComposeFiles = ["docker-compose-snapshots-mysql-binlog.yml"]
        startedServices = ["cdcservice"]
        removeContainers = project.ext.removeContainers
    }

    postgrespolling {
        projectName = null
        useComposeFiles = ["docker-compose-postgres-polling.yml"]
        removeContainers = project.ext.removeContainers
    }

    postgrespollinginfrastructure {
        projectName = null
        useComposeFiles = ["docker-compose-postgres-polling.yml"]
        startedServices = ["cdcservice"]
        removeContainers = project.ext.removeContainers
    }

    postgreswal {
        projectName = null
        useComposeFiles = ["docker-compose-postgres-wal.yml"]
        removeContainers = project.ext.removeContainers
    }

    postgreswalinfrastructure {
        projectName = null
        useComposeFiles = ["docker-compose-postgres-wal.yml"]
        startedServices = ["cdcservice"]
        removeContainers = project.ext.removeContainers
    }

    javaDevelopment {
        projectName = null
        useComposeFiles = ["docker-compose-java-development-build.yml"]
    }
    javaDevelopmentImage {
        projectName = null
        useComposeFiles = ["java-development/docker-compose-java-development-build.yml"]
    }
}

task prepareJavaDevelopmentDockerImage(type:Exec) {
    commandLine './prepare-java-development-docker-image.sh'
}

javaDevelopmentImageComposeBuild.dependsOn(prepareJavaDevelopmentDockerImage)

subprojects.each {
  if (it.name.endsWith("-service") || it.name.endsWith("-gateway")) {
    mysqlbinlogComposeUp.dependsOn(":" + it.name + ":assemble")
  }
 }

mysqlbinlogComposeUp.dependsOn(mysqlbinloginfrastructureComposeUp)

task buildAndStartServicesMySql(type: GradleBuild) {
    tasks = ["mysqlbinlogComposeUp"]
}

task endToEndTests(type: GradleBuild) {
    tasks = [":end-to-end-tests:test"]
}

endToEndTests.dependsOn(mysqlbinlogComposeUp)
endToEndTests.dependsOn(":end-to-end-tests:cleanTest")

task stopServicesMySql(type: GradleBuild) {
    tasks = ["mysqlbinlogComposeDown"]
}
